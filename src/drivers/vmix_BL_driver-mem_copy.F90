!BOP

! !ROUTINE: vmix_BL_driver_mem_copy

! !DESCRIPTION: A stand-alone driver for the CVMix package. This particular
!  driver generates the Bryan-Lewis coefficients in two columns, one that
!  represents tropical latitudes and one that represents subtropical
!  latitudes. All memory is declared in the driver and then copied into the
!  CVMix data structures.
!\\
!\\

! !REVISION HISTORY:
!  SVN:$Id$
!  SVN:$URL$

! !INTERFACE:

Program vmix_BL_driver_mem_copy

! !USES:

  use vmix_kinds_and_types, only : vmix_r8,                 &
                                   vmix_data_type,          &
                                   vmix_global_params_type, &
                                   vmix_bkgnd_params_type
  use vmix_background,      only : vmix_init_bkgnd,         &
                                   vmix_coeffs_bkgnd
  use vmix_put_get,         only : vmix_put
  use vmix_output,          only : vmix_output_open,        &
                                   vmix_output_write,       &
                                   vmix_output_close
!EOP
!BOC

  Implicit None

  type (vmix_data_type)         , dimension(2) :: Vmix_vars
  type (vmix_global_params_type)               :: Vmix_params
  type (vmix_bkgnd_params_type) , dimension(2) :: Vmix_BL_params

  ! iface_depth is the depth of each interface;  same in both columns
  real(kind=vmix_r8), dimension(:),     allocatable :: iface_depth

  ! Global parameter
  integer, parameter :: ncol = 2
  ! array indices
  integer :: icol,kw
  ! file indices
#ifdef _NETCDF
  integer :: fid
#else
  integer :: fid1, fid2, fid3
#endif

  ! Namelist variables
  ! 1) General mixing parameters
  integer                    :: nlev      ! number of levels for column
  real(kind=vmix_r8)         :: ocn_depth ! Depth of ocn
  ! 2) Bryan-Lewis mixing parameters for column 1
  real(kind=vmix_r8)         :: col1_vdc1, col1_vdc2, col1_linv, col1_dpth
  ! 3) Bryan-Lewis mixing parameters for column 2
  real(kind=vmix_r8)         :: col2_vdc1, col2_vdc2, col2_linv, col2_dpth

  ! Namelists that may be read in, depending on desired mixing scheme
  namelist/cvmix_nml/nlev, ocn_depth
  namelist/BryanLewis1_nml/col1_vdc1, col1_vdc2, col1_linv, col1_dpth
  namelist/BryanLewis2_nml/col2_vdc1, col2_vdc2, col2_linv, col2_dpth

  ! Read general mixing parameters
  read(*, nml=cvmix_nml)

  ! Calculate depth of cell interfaces based on number of levels and ocean
  ! depth
  allocate(iface_depth(nlev+1))
  iface_depth(1) = 0.0d0
  do kw = 2,nlev+1
    iface_depth(kw) = iface_depth(kw-1) + ocn_depth/dble(nlev)
  end do

  ! Initialization for CVMix data types
  ! Note that vmix_put will allocate memory for arrays as needed
  call vmix_put(Vmix_params,  'max_nlev', nlev)
  call vmix_put(Vmix_params,  'prandtl',  0.0d0)
  do icol=1,2
    call vmix_put(Vmix_vars(icol), 'nlev',     nlev)
    call vmix_put(Vmix_vars(icol), 'visc',     0.0d0)
    call vmix_put(Vmix_vars(icol), 'diff',     0.0d0)
    call vmix_put(Vmix_vars(icol), 'zw',       iface_depth)
  end do

  ! Read / set B-L parameters for column 1
  read(*, nml=BryanLewis1_nml)
  call vmix_init_bkgnd(Vmix_vars(1), Vmix_params, Vmix_BL_params(1),     &
                       col1_vdc1, col1_vdc2, col1_linv, col1_dpth)
  call vmix_coeffs_bkgnd(Vmix_vars(1), Vmix_BL_params(1), 1)
  
  ! Read / set B-L parameters for column 2
  read(*, nml=BryanLewis2_nml)
  call vmix_init_bkgnd(Vmix_vars(2), Vmix_params, Vmix_BL_params(2),     &
                       col2_vdc1, col2_vdc2, col2_linv, col2_dpth)
  call vmix_coeffs_bkgnd(Vmix_vars(2), Vmix_BL_params(2), 1)
  
  ! Output
#ifdef _NETCDF
  ! data will have diffusivity from both columns (needed for NCL script)
  call vmix_output_open(fid, "data.nc", "nc")
  ! Note: all entries in string of variables to output must be
  !       the same length... hence the space in "diff "
  call vmix_output_write(fid, Vmix_vars, (/"depth", "diff "/))
  call vmix_output_close(fid)
#else
  ! data will have diffusivity from both columns (needed for NCL script)
  call vmix_output_open(fid1, "data.out", "ascii")
  ! col1 will just have diffusivity from low lat
  call vmix_output_open(fid2, "col1.out", "ascii")
  ! col2 will just have diffusivity from high lat
  call vmix_output_open(fid3, "col2.out", "ascii")

  ! Note: all entries in string of variables to output must be
  !       the same length... hence the space in "diff "
  call vmix_output_write(fid1, Vmix_vars,    (/"depth", "diff "/))
  call vmix_output_write(fid2, Vmix_vars(1), (/"depth", "diff "/))
  call vmix_output_write(fid3, Vmix_vars(2), (/"depth", "diff "/))

  call vmix_output_close(fid1)
  call vmix_output_close(fid2)
  call vmix_output_close(fid3)
#endif

!EOC

End program vmix_BL_driver_mem_copy
