!BOP

! !ROUTINE: vmix_BL_driver_pointers

! !DESCRIPTION: A stand-alone driver for the CVMix package. This particular
!  driver generates the Bryan-Lewis coefficients in two columns, one that
!  represents tropical latitudes and one that represents subtropical
!  latitudes. All memory is declared in the driver, and the CVMix data type
!  points to the local variables.
!\\
!\\

! !REVISION HISTORY:
!  SVN:$Id$
!  SVN:$URL$

! !INTERFACE:

Program vmix_BL_driver_pointers

! !USES:

  use vmix_kinds_and_types
  use vmix_background
  use vmix_convection
  use vmix_put_get
  use vmix_output
!\newpage
!EOP
!BOC

  Implicit None

  type (vmix_data_type)         , dimension(2) :: Vmix_vars
  type (vmix_global_params_type)               :: Vmix_params
  type (vmix_bkgnd_params_type) , dimension(2) :: Vmix_BL_params

  ! Will use 2 columns, viscosity will be 2 x nlev+1 and diffusivity will 
  ! be 2 x nlev+1 x 1 (diffusivity is 2D in column)
  ! iface_depth is the depth of each interface;  same in both columns
  real(kind=vmix_r8), dimension(:),     allocatable, target :: iface_depth
  real(kind=vmix_r8), dimension(:,:),   allocatable, target :: viscosity
  real(kind=vmix_r8), dimension(:,:,:), allocatable, target :: diffusivity

  ! Global parameter
  integer, parameter :: ncol = 2
  ! array indices
  integer :: icol,kw
  ! file indices
  integer :: fid1, fid2, fid3

  ! Namelist variables
  ! 1) General mixing parameters
  integer                    :: nlev      ! number of levels for column
  real(kind=vmix_r8)         :: ocn_depth ! Depth of ocn
  ! 2) Bryan-Lewis mixing parameters for column 1
  real(kind=vmix_r8)         :: col1_vdc1, col1_vdc2, col1_linv, col1_dpth
  ! 3) Bryan-Lewis mixing parameters for column 2
  real(kind=vmix_r8)         :: col2_vdc1, col2_vdc2, col2_linv, col2_dpth

  ! Namelists that may be read in, depending on desired mixing scheme
  namelist/cvmix_nml/nlev, ocn_depth
  namelist/BryanLewis1_nml/col1_vdc1, col1_vdc2, col1_linv, col1_dpth
  namelist/BryanLewis2_nml/col2_vdc1, col2_vdc2, col2_linv, col2_dpth

  ! Read general mixing parameters
  read(*, nml=cvmix_nml)

  ! Calculate depth of cell interfaces based on number of levels and ocean
  ! depth (also allocate memory for diffusivity and viscosity)
  allocate(iface_depth(nlev+1))
  iface_depth(1) = 0.0d0
  do kw = 2,nlev+1
    iface_depth(kw) = iface_depth(kw-1) + ocn_depth/dble(nlev)
  end do

  ! Allocate memory to store viscosity and diffusivity values
  allocate(diffusivity(2,nlev+1,1), viscosity(2,nlev+1))

  ! Initialization for CVMix data types
  call vmix_put(Vmix_params,  'max_nlev', nlev)
  call vmix_put(Vmix_params,  'prandtl',  0.0d0)
  do icol=1,2
    call vmix_put(Vmix_vars(icol), 'nlev',     nlev)
    ! Point Vmix_vars values to memory allocated above
    Vmix_vars(icol)%visc_iface => viscosity(icol,:)
    Vmix_vars(icol)%diff_iface => diffusivity(icol,:,:)
    Vmix_vars(icol)%z_iface => iface_depth
  end do

  ! Read / set B-L parameters for column 1
  read(*, nml=BryanLewis1_nml)
  call vmix_init_bkgnd(Vmix_vars(1), Vmix_params, Vmix_BL_params(1),     &
                       col1_vdc1, col1_vdc2, col1_linv, col1_dpth)
  call vmix_coeffs_bkgnd(Vmix_vars(1), Vmix_BL_params(1), 1)
  
  ! Read / set B-L parameters for column 2
  read(*, nml=BryanLewis2_nml)
  call vmix_init_bkgnd(Vmix_vars(2), Vmix_params, Vmix_BL_params(2),     &
                       col2_vdc1, col2_vdc2, col2_linv, col2_dpth)
  call vmix_coeffs_bkgnd(Vmix_vars(2), Vmix_BL_params(2), 1)
  
  ! Output
  ! data will have diffusivity from both columns (needed for NCL script)
#ifdef _NETCDF
  call vmix_output_open(fid1, "data.nc", "nc")
#else
  call vmix_output_open(fid1, "data.out", "ascii")
#endif

  ! Note: all entries in string of variables to output must be
  !       the same length... hence the space in "diff "
  call vmix_output_write(fid1, Vmix_vars, (/"depth", "diff "/))
  call vmix_output_close(fid1)

#ifndef _NETCDF
  ! col1 will just have diffusivity from low lat
  call vmix_output_open(fid2, "col1.out", "ascii")
  ! col2 will just have diffusivity from high lat
  call vmix_output_open(fid3, "col2.out", "ascii")
  call vmix_output_write(fid2, Vmix_vars(1), (/"depth", "diff "/))
  call vmix_output_write(fid3, Vmix_vars(2), (/"depth", "diff "/))
  call vmix_output_close(fid2)
  call vmix_output_close(fid3)
#endif

!EOC

End program vmix_BL_driver_pointers
